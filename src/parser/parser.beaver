%class "LangParser";
%package "lang.ast";

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		throw new SyntaxError("Cannot recover from the syntax error");
	}
:};

%terminals COLON, SEP, LBRACKET, RBRACKET, LPAREN, RPAREN, ID, UPPERID, LOWERID, COMMA;

%typeof ruleset = "RuleSet";
%typeof rule_list = "List<Rule>";
%typeof premises_list = "List<Formula>";
%typeof rule = "Rule";
%typeof formula = "Formula";
%typeof term = "Term";
%typeof tyterm = "TyTerm";
%typeof term_list = "List<Term>";
%typeof id = "String";
%typeof upperid = "String";
%typeof lowerid = "String";

%goal ruleset;

ruleset = rule_list.a {: return new RuleSet(a); :};

rule_list =
	  rule.a {: return new List().add(a); :}
	| rule_list.a rule.b {: return a.add(b); :}
	;

premises_list =
	  formula.a {: return new List().add(a); :}
	| premises_list.a COMMA formula.b {: return a.add(b); :}
	;

rule =
    LBRACKET upperid.id RBRACKET premises_list.premises SEP formula.conclusion {: return new Rule(id, conclusion, premises); :}
//| LBRACKET upperid.id RBRACKET SEP formula.conclusion {: return new Rule(id, conclusion, new List()); :}
  | LBRACKET upperid.id RBRACKET formula.conclusion {: return new Rule(id, conclusion, new List()); :}
  ;

formula = term.expr COLON tyterm.ty {: return new HasType(expr, ty); :};

term =
    upperid.rulename LPAREN term_list.params RPAREN {: return new Function(rulename, params); :}
  | upperid.rulename LPAREN RPAREN {: return new Function(rulename, new List()); :}
  | upperid.id {: return new Value(id); :}
  | lowerid.id {: return new Value(id); :}
  ;

tyterm =
    upperid.id {: return new TyVal(id); :}
  | lowerid.id {: return new TyVar(id); :}
  ;

term_list =
	  term.a {: return new List().add(a); :}
	| term_list.a COMMA term.b {: return a.add(b); :}
	;

//id = ID;
upperid = UPPERID;
lowerid = LOWERID;

